# Set the base directory and crate directories
base_dir := justfile_directory()
crates_dir := base_dir / "crates"
pdk_test_runner_dir := crates_dir / "pdk-test-runner"
pdk_mock_plugin_dir := crates_dir / "pdk-mock-plugin"
pdk_mock_host_dir := crates_dir / "pdk-mock-host"

# Define target paths
target := "wasm32-unknown-unknown/release"
mock_plugin_wasm := pdk_mock_plugin_dir / "target" / target / "anything_pdk_mock_plugin.wasm"
mock_host_wasm := pdk_mock_host_dir / "target" / target / "anything_pdk_mock_host.wasm"
test_runner_wasm := pdk_test_runner_dir / "target" / target / "anything_pdk_test_runner.wasm"

default:
  @just --list --justfile {{justfile()}}

all: build-all test

build-all: build-mock-plugin build-mock-host build-test-runner

build-mock-plugin: 
  cd {{ pdk_mock_plugin_dir }} && cargo build --release

build-mock-host: 
  cd {{ pdk_mock_host_dir }} && cargo build --release

build-test-runner: 
  cd {{ pdk_test_runner_dir }} && cargo build --release

# Test Mock Plugin with Mock Host using the test runner
test:
  cd {{crates_dir}} && xtp plugin test {{mock_plugin_wasm}} --with {{test_runner_wasm}} --mock-host {{mock_host_wasm}}